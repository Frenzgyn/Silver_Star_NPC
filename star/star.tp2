BACKUP ~star/backup~

//AUTHOR ~Olryx2@Yahoo.co.uk~
SUPPORT ~SHS Staff at http://www.shsforums.net/forum/127-mod-resurrections~

VERSION ~v2.0.0~

README ~star/readme-star.html~


ALLOW_MISSING ~ar4500.bcs~
              ~fatesp.dlg~


ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

		/* ----------------------------------------- *
		 *    Determines which game is installed.    *
		 * ----------------------------------------- */

		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0                       // b2gee
		OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                         // eet
		OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0                   // EE game
		OUTER_SET is_tobex = (FILE_EXISTS ~tobex_ini/tobexcore.ini~) ? 1 : 0 // ToBEx is installed

		/* ------------------------------------------------------- *
		 *    Sets default variables if they're not set already    *
		 * ------------------------------------------------------- */

		// Variable used to properly install specific EE games ressources
		ACTION_IF (is_ee) BEGIN
			OUTER_SPRINT ee_file "_ee"
		END ELSE BEGIN
			OUTER_SPRINT ee_file ""
		END

		// Variable used to properly install Sebastian kit
		ACTION_IF (is_ee OR is_tobex) BEGIN
			OUTER_SET kits_lim_256  = 0
		END ELSE BEGIN
			OUTER_SET kits_lim_256  = 1
		END

		/* ------------------------------------------- *
		 *    EET chapters continuity compatibility    *
		 * ------------------------------------------- */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END

		/* --------------------------------------------------------------------------------------------- *
		 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
		 *    No exception, everything needs to be converted                                             *
		 *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
		 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY d#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY d#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = d#noconvert
				reload_array = d#reload
		END

	END

END


AUTO_TRA ~star/lang/%s~

// Languages
LANGUAGE ~English~
         ~english~
         ~star/lang/english/setup.tra~

LANGUAGE ~Deutsch~
         ~german~
         ~star/lang/german/setup.tra~

LANGUAGE ~Russian~
         ~russian~
         ~star/lang/russian/setup.tra~


/* ====================================================== *
 *    Silver Star NPC Mod for BGII:SoA  (main component)  *
 * ====================================================== */

BEGIN @1 // ~Silver Star NPC Mod for BGII:SoA~
LABEL ~d#_silverstar_npc_main~
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @2 // ~You must have BG2, ToB, BGT, BG2:EE or EET installed to install the Silver Star NPC mod.~

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~
